java {
    withJavadocJar()
    withSourcesJar()
}

def versionName = publicationVersionName
def artifactName = project.name
project.group = projectGroupId
project.version = versionName

publishing {
    repositories {
        maven {
            def releaseRepo = "https://oss.sonatype.org/service/local/staging/deploy/maven2/"
            def snapshotRepo = "https://oss.sonatype.org/content/repositories/snapshots/"
            url = versionName.contains("-") ? snapshotRepo : releaseRepo
            credentials {
                username = project.hasProperty('ossrh.username')
                        ? project.property("ossrh.username")
                        : System.getenv("OOSRH_USERNAME")
                password = project.hasProperty('ossrh.password')
                        ? project.property("ossrh.password")
                        : System.getenv("OOSRH_PASSWORD")
            }
        }
    }
    publications {
        ossrh(MavenPublication) {
            from components.java

            // these are not necessary since withJavadocJar and withSourcesJar
            // bundle artifacts with components.java
            //artifact sourcesJar
            //artifact javadocJar

            groupId = projectGroupId
            artifactId = artifactName
            version = versionName

            pom {
                name = pomName
                description = pomDescription
                url = pomUrl
                licenses {
                    license {
                        name = pomLicence
                        url = pomLicenceUrl
                    }
                }
                developers {
                    developer {
                        id = pomDeveloperId
                        name = pomDeveloperName
                        email = pomDeveloperEmail
                    }
                }
                scm {
                    connection = pomScmConnection
                    developerConnection = pomScmConnection
                    url = pomScmUrl
                }
            }
        }
    }
}

signing {
    sign publishing.publications.ossrh
}

dependencies {

    compileOnly(gradleApi())
    compileOnly("com.android.tools.build:gradle:${android_version}")
    compileOnly("org.jetbrains.kotlin:kotlin-gradle-plugin:${kotlin_version}")

}